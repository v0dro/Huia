String  = Huia.requireCore: 'string'
Integer = Huia.requireCore: 'integer'

def: 'toString' as:
  str = Ruby.createFromObject: self
  .send: 'to_s'
  String.createFromValue: str

def: 'at:' as: |index|
  Ruby.createFromObject: self
  .send: '[]' withArgs: [index]

def: 'at:set:' as: |index,value|
  Ruby.createFromObject: self
  .send: '[]=' withArgs: [index,value]

  value

def: 'size' as:
  size = Ruby.createFromObject: self
  .send: 'size'
  .unwrap
  Integer.createFromValue: size

def: 'inspect' as:
  result = '['

  last = self.size

  Ruby.createFromObject: self
  .send: 'each_with_index' withBlock: |item,index|
    index = Integer.createFromValue: index
    index = index + 1

    result.push: ' '
    result.push: item.inspect

    if: (last == index) then:
      result.push: ' '

    if: (last != index) then:
      result.push: ','

  result.push: ']'
